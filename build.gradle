import org.apache.commons.io.FileUtils

buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
		classpath 'commons-io:commons-io:2.4'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'

final int NUMBER_OF_TESTS = 3

def createTestTask(int testNumber, String testName, String mainClass, String inputFile, String processFile, String outputFile, String expectedOutputFile, String patchFormat) {
	Task testTask = task("test${testName}${testNumber}") << {
		File output = file("test/${outputFile}${testNumber}.txt")
		if (output.exists()) {
			output.delete()
		}
		
		String[] args = [ 'java', '-cp', "${jar.archivePath}", mainClass, "test/${inputFile}${testNumber}.txt", "test/${processFile}${testNumber}.txt", "test/${outputFile}${testNumber}.txt" ]
		if (patchFormat != null) {
			String[] newArgs = new String[args.length + 1];
			System.arraycopy(args, 0, newArgs, 0, args.length);
			newArgs[args.length] = patchFormat;
			args = newArgs;
		}
 		Process process = new ProcessBuilder(args).inheritIO().directory(file('.')).start()
		int exitCode = process.waitFor()
		if (exitCode != 0) {
			println "Test failed, process exited with exitcode ${exitCode}"
		} else if (!FileUtils.contentEquals(output, file("test/${expectedOutputFile}${testNumber}.txt"))) {
			println 'Test failed, output file contents did not match the expected file contents'
		} else {
			println 'Test successful'
		}
	}
	testTask.dependsOn jar
	return testTask
}

def createTestDiffTask(int testNumber) {
	final String[] PATCH_FORMATS = [
		"normal",
		"java",
		"byte"
	]
	return createTestTask(testNumber, 'Diff', 'net.earthcomputer.meme.diff.DiffFinder', 'base', 'work', 'patchTestOutput', 'patch', PATCH_FORMATS[testNumber - 1])
}

def createTestPatchTask(int testNumber) {
	return createTestTask(testNumber, 'Patch', 'net.earthcomputer.meme.diff.Patcher', 'base', 'patch', 'workTestOutput', 'work', null)
}

task testAllDiff
task testAllPatch
task testAll
testAll.dependsOn testAllDiff
testAll.dependsOn testAllPatch
test.dependsOn testAll

for (int i = 1; i <= NUMBER_OF_TESTS; i++) {
	testAllDiff.dependsOn createTestDiffTask(i)
	testAllPatch.dependsOn createTestPatchTask(i)
}

archivesBaseName = 'memediff'
group = 'net.earthcomputer.meme'
version = '1.0'

jar {
	manifest {
		attributes 'Main-Class': 'net.earthcomputer.meme.diff.DiffFinder'
	}
}
